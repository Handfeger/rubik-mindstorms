apply plugin: 'java'

subprojects {
    apply plugin: 'java'

    sourceSets {
        main {
            java {
                srcDir 'src'
            }
        }
    }
}

def libDir = './lib/'
def lejosHome = '/home/root/lejos'
def lejosProgs = '/home/lejos/programs'

def mainClass = 'Main'

sourceCompatibility = 1.7
version = '0.1'

repositories {
    mavenCentral()
}

configurations {
    sshAntTask
    provided
}

dependencies {
    sshAntTask 'org.apache.ant:ant-jsch:1.9.4'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile files(libDir + 'ev3built/ev3classes.jar')
}

jar {
    manifest {
        attributes(
                'Implementation-Title': "Gradle",
                'Implementation-Version': version,
                'Class-Path': "${lejosHome}/lib/ev3classes.jar ${lejosHome}/libjna/usr/share/java/jna.jar ${lejosHome}/lib/groovy-all-2.2.2.jar",
                'Main-Class': mainClass
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task deployEV3(dependsOn: 'build') << {
    def knownHosts = File.createTempFile('knownhosts', 'txt')
    ant.taskdef(
            name: 'scp',
            classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
            classpath: configurations.sshAntTask.asPath
    )

    ant.scp(
            todir: ev3_username + '@' + ev3_server + ':' + lejosProgs,
            password: ev3_password,
            verbose: 'true',
            trust: true,
            knownhosts: knownHosts
    ) {
        fileset (dir: './build/libs') {
            include(name: '**/*.jar')
        }
    }
}